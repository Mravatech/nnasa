apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

def globalConfiguration = rootProject.extensions.getByName("ext")

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools

    testBuildType "debug"

    defaultConfig {
        applicationId deps.applicationId
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        vectorDrawables.useSupportLibrary = true

        def versionMajor = 4
        def versionMinorPlusPatch = 21 // 99 max please
        versionName "${versionMajor}.${versionMinorPlusPatch}"
        versionCode (versionMajor * 100 + versionMinorPlusPatch)

        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding "UTF-8"
    }

    kotlin {
        experimental { coroutines 'enable' }
        androidExtensions { experimental = true }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'protobuf.meta'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions { abortOnError false }

    signingConfigs {
        debug {
            storeFile file("../keystore/key.jks")
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
        stage {
            storeFile file("../keystore/key.jks")
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
        release {
            storeFile "$System.env.STORE_FILE_LOCATION" as File
            storePassword "$System.env.STORE_PASSWORD"
            keyAlias "$System.env.KEY_ALIAS"
            keyPassword "$System.env.KEY_PASSWORD"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        stage {
            applicationIdSuffix ".stage"
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.stage
        }
        debug {
            applicationIdSuffix ".dev"
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "access"

    productFlavors {
        fbi {
            dimension "access"
            versionNameSuffix "-fbi"
        }
        normal {
            dimension "access"
        }
    }

    applicationVariants.all { variant ->
        def variantNameLower = variant.getName().toLowerCase()
        if (variantNameLower.contains("release")) {
            variant.buildConfigField "String", "ENDPOINT", "\"${globalConfiguration['endpointProd']}\""
            variant.buildConfigField "String", "TERMS_AND_CONDITIONS", "\"${globalConfiguration['termsAndConditionsProd']}\""
        } else if (variantNameLower.contains("stage")) {
            variant.buildConfigField "String", "ENDPOINT", "\"${globalConfiguration['endpointPreProd']}\""
            variant.buildConfigField "String", "TERMS_AND_CONDITIONS", "\"${globalConfiguration['termsAndConditionsPreProd']}\""
        } else if (variantNameLower.contains("debug")) {
            variant.buildConfigField "String", "ENDPOINT", "\"${globalConfiguration['endpointDev']}\""
            variant.buildConfigField "String", "TERMS_AND_CONDITIONS", "\"${globalConfiguration['termsAndConditionsDev']}\""
        }

        variant.buildConfigField "String", "URL_GOOGLE_MAPS_SEARCH", "\"${deps.urls.google_maps_search}\""
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res/controller',
                    'src/main/res/widget',
                    'src/main/res/item',
                    'src/main/res/activity',
            ]
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //multidex
    implementation deps.multidex.multidex

    //modules
    implementation deps.core
    implementation deps.data
    implementation deps.domain

    //support
    implementation deps.support.appcompat
    implementation deps.support.design
    implementation deps.support.cardview
    implementation deps.support.annotations
    implementation deps.support.recyclerview
    implementation deps.support.constraint
    implementation deps.support.coordinator

    //firebase
    implementation deps.firebase.core
    implementation deps.firebase.auth
    implementation deps.firebase.auth_service
    implementation deps.firebase.database
    implementation deps.firebase.storage
    implementation deps.firebase.fcm
    implementation deps.firebase.firestore
    implementation deps.firebase.ui

    //play
    implementation deps.playServices.places
    implementation deps.playServices.locations

    //coroutines
    implementation deps.coroutines.core
    implementation deps.coroutines.android

    //glide
    implementation deps.glide.glide
    kapt deps.glide.glideCompiler
    implementation deps.glide.glideTransformers
    implementation deps.glide.glideOkHttp

    //ucrop
    implementation deps.ucrop.general

    //arch
    implementation deps.archComponents.viewModel

    //conductor
    implementation deps.conductor.core
    implementation deps.conductor.lifecycle
    implementation deps.conductor.support

    //dependency injection
    implementation deps.di.core
    implementation deps.di.android

    implementation deps.archComponents.lifecycleExt
    implementation deps.archComponents.runtime
    implementation deps.archComponents.common

    //other
    implementation deps.other.logger
    debugImplementation deps.other.leakDebug
    stageImplementation deps.other.leakDebug
    releaseImplementation deps.other.leakRelease
    implementation deps.stetho.runtime
    implementation deps.stetho.http

    //network
    implementation deps.network.retrofit
    implementation deps.network.coroutineAdapter
    implementation deps.network.gsonconverter
    implementation deps.network.logging

    //ui
    implementation deps.ui.bigImageViewer
    implementation deps.ui.bigImageViewerGlideLoader
    implementation deps.ui.bigImageViewerGlideFactory
    implementation deps.ui.exoplayer
    implementation deps.ui.bottomnavigation
    implementation deps.ui.tablayout
    implementation deps.ui.dialogs
    implementation deps.ui.fab
    implementation deps.ui.durationPicker
    implementation deps.ui.pagerIndicator
    implementation deps.ui.chipsLayoutManager
    implementation deps.ui.timeAgo
    implementation (deps.ui.navigationDrawer) {
        transitive = true
    }

    //kotlin
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.androidExtensions

    //crashlytics
    implementation (deps.other.crashlytics)
}

apply plugin: 'com.google.gms.google-services'
